////////////////////////////////////////////////////////////////////////////////
// List
////////////////////////////////////////////////////////////////////////////////

uses <algs>

class List(class):
  protected:
    var Items, Size

  public:
    proc Create, Free

    proc Add, Insert, Delete, Clear
    func Get, IndexOf

    proc Sort
    func NextIns, Find
end


proc List::Create():
  $Items ?= new[0]
  $Size ?= new(0)
end


proc List::Free():
  Free($Items, $Size, $)
end


proc List::Add(Obj):
  SetLen($Items, $Size + 1)
  $Items[$Size] ?= Obj
  $Size++
end


proc List::Insert(Obj, Index):
  if ($Size < Index) | (Index < 0):
    raise "List index (" + Index + ") out of bounds."
  else:
    $Size++
    SetLen($Items, $Size)
    for(i ?= $Size - 1; i > Index; i--):
      $Items[i] ?= $Items[i - 1]
    end
    $Items[Index] ?= Obj
  end
end


proc List::Delete(Index):
  if ($Size <= Index) | (Index < 0):
    raise "List index (" + Index + ") out of bounds."
  else:
    $Size--
    for(; Index < $Size; Index++):
      $Items[Index] ?= $Items[Index + 1]
    end
    SetLen($Items, $Size)
  end
end


proc List::Clear():
  SetLen($Items, 0)
  $Size = 0
end


func List::Get(Index):
  return $Items[Index]
end


func List::IndexOf(Obj):
  if typeof(Obj) in [TypeArray, TypeClass]:
    p ?= @Obj
    for(i ?= 0; i < $Size; i++):
      if p == @$Items[i]:
        return i
      end
    end
  else:
    for(i ?= 0; i < $Size; i++):
      if Obj == $Items[i]:
        return i
      end
    end
  end
end

proc List::Sort():
  QuickSort($Items)
end

func List::NextIns(Obj):
  Indx ?= BinNext($Items, Obj)
  $Insert(Obj, Indx)
  return Indx
end

func List::Find(Obj):
  return BinSearch($Items, Obj)
end
